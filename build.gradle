/*
 * Copyright (C) 2018 Knot.x Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'java-library'
  id 'io.spring.dependency-management' version '1.0.5.RELEASE'
  id 'maven'
}

sourceCompatibility = '1.8'

switch (JavaVersion.current()) {
  case JavaVersion.VERSION_1_8:
    targetCompatibility = '1.8'
    break
  case JavaVersion.VERSION_1_9:
    targetCompatibility = '9'
    break
  case JavaVersion.VERSION_1_10:
    targetCompatibility = '10'
    break
  case JavaVersion.VERSION_11:
    targetCompatibility = '11'
    break
  default:
    throw new GradleException("This build must be run with java 8,9,10 or 11")
    break
}

println 'Compilation target is JDK '+ targetCompatibility

allprojects {
  group 'io.knotx'

  repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://plugins.gradle.org/m2/' }
    maven { url "https://oss.sonatype.org/content/groups/staging/" }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
  }
}

description = 'Knot.x JUnit5 test module'

defaultTasks = ['build']

dependencyManagement {
  imports {
    mavenBom "io.knotx:knotx-dependencies:${project.version}"
    mavenBom 'org.junit:junit-bom:5.3.1'
  }
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
  options.compilerArgs << '-Xlint:deprecation'
  options.incremental = true
}


dependencies {
  compile 'io.vertx:vertx-core'
  compile 'io.vertx:vertx-service-proxy'
  compile 'io.vertx:vertx-rx-java2'
  compile 'io.vertx:vertx-config'
  compile 'io.vertx:vertx-config-hocon'
  compile 'io.vertx:vertx-junit5'

  compile 'org.junit.jupiter:junit-jupiter-api'
  compile 'org.junit.jupiter:junit-jupiter-params'
  compile 'org.junit.jupiter:junit-jupiter-migrationsupport'

  compile 'org.mockito:mockito-core'
  compile 'org.mockito:mockito-junit-jupiter'
  compile 'com.github.tomakehurst:wiremock'

  compile 'me.alexpanov:free-port-finder'
  compile 'commons-io:commons-io'
  compile 'org.jsoup:jsoup'
}

test {
  systemProperties = [
      'vertx.logger-delegate-factory-class-name': 'io.vertx.core.logging.SLF4JLogDelegateFactory'
  ]
}

apply from: "${rootProject.projectDir}/release.gradle"
